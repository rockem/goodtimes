apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'

//build stuff
sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime
            args = ['-f', 'pretty', '--glue', 'src/test/groovy', 'src/test/resources']

        }
    }
}

dependencies {
    // Groovy library for groovy building!
    compile 'org.codehaus.groovy:groovy-all:2.4.0'

    /*
   In order to work around a really flagrant bug ( http://issues.gradle.org/browse/GRADLE-732 )
   You have to create a configuration that includes the jar.archivePath (the jar.archivePath is created by the java plugin)
   and give it the same name as the cucumberRuntime configuration, or name it something different and have the cucumberRuntime
   configuration extend from it as well.
   VERY ANNOYING BUG
    */
    cucumberRuntime files("${jar.archivePath}")

    testCompile 'junit:junit:4.11'
    testCompile 'info.cukes:cucumber-junit:1.1.4'
    testCompile 'info.cukes:cucumber-groovy:1.1.4'
}

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}